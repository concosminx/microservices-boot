buildscript {

  ext {
    springBootVersion = '3.1.2'
    springDepMgmtVersion = '1.1.2'
    jibVersion = '3.3.2'
  }

  repositories {
    mavenCentral()
    maven {
      url = uri("https://plugins.gradle.org/m2/")
    }
  }

  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    classpath "io.spring.gradle:dependency-management-plugin:${springDepMgmtVersion}"
    classpath "com.google.cloud.tools:jib-gradle-plugin:${jibVersion}"
  }

}


subprojects {

  group = 'com.example'
  version = '0.0.1-SNAPSHOT'

  repositories {
    mavenCentral()
  }


  apply plugin: "java"
  apply plugin: "org.springframework.boot"
  apply plugin: "io.spring.dependency-management"
  apply plugin: "com.google.cloud.tools.jib"



  java {
    sourceCompatibility = '17'
  }

  configurations {
    compileOnly {
      extendsFrom annotationProcessor
    }
  }

  ext {
    set('springCloudVersion', "2022.0.4")
  }

  dependencyManagement {
    imports {
      mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
  }

  if (!project.name.contains("api-gateway")) {
    dependencies {
      implementation 'org.springframework.boot:spring-boot-starter-web'
    }
  }

  dependencies {
    implementation 'io.micrometer:micrometer-tracing-bridge-brave'
    implementation 'io.zipkin.reporter2:zipkin-reporter-brave'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    runtimeOnly 'io.micrometer:micrometer-registry-prometheus'
  }

  tasks.named('test') {
    useJUnitPlatform()
  }

  jib.to.image = "microservices-tutorial/${project.name}"
  jib.to.auth.username = System.getenv('DOCKER_USERNAME')
  jib.to.auth.password  = System.getenv('DOCKER_PASSWORD')

  jib.from.image = 'eclipse-temurin:17.0.4.1_1-jre'
  jib.from.auth.username = System.getenv('DOCKER_USERNAME')
  jib.from.auth.password  = System.getenv('DOCKER_PASSWORD')

}